apply plugin: 'com.android.application'

android {
    compileSdkVersion 16
    buildToolsVersion "22.0.1"

    defaultConfig {
        applicationId "org.osmtracker"
        minSdkVersion 8
        targetSdkVersion 16

        testApplicationId "org.osmtracker.test"
        testInstrumentationRunner "android.test.InstrumentationTestRunner"
    }

    signingConfigs {


        // Create a file $HOME/.gradle/gradle.properties
        // containing the values signing.storeFile=..., etc.
        if (project.hasProperty('signing.storeFile')) {
            release {
                storeFile file(project.property('signing.storeFile'))
                storePassword project.property('signing.storePassword')
                keyAlias project.property('signing.keyAlias')
                keyPassword project.property('signing.keyPassword')
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
            if (signingConfigs.hasProperty('release')) {
                signingConfig signingConfigs.release
            }


        }
    }

    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }

}

dependencies {
    compile 'org.apache.james:apache-mime4j-core:0.7.2'
    compile 'org.apache.httpcomponents:httpclient:4.2.1'
    compile 'org.apache.httpcomponents:httpcore:4.2.1'
    compile 'org.apache.httpcomponents:httpmime:4.2.1'
    compile 'org.osmdroid:osmdroid-android:5.5:release@aar'
    compile 'oauth.signpost:signpost-commonshttp4:1.2.1.2'
    compile 'oauth.signpost:signpost-core:1.2.1.2'
    compile 'org.slf4j:slf4j-android:1.6.1-RC1'
}

task copyNorwegianValues(type: Copy) {
    from 'src/main/res/values-nb/'
    into 'src/main/res/values-no/'
}

task copyNorwegianXml(type: Copy) {
    from 'src/main/res/xml-nb/'
    into 'src/main/res/xml-no/'
}

android.applicationVariants.all { variant ->
    variant.outputs.each { output ->
        // Copying 'nb' resources to 'no' (See pull request #6)
        output.processResources.dependsOn([copyNorwegianValues, copyNorwegianXml])
    }
}
